////////////////////////////////////////////////////////////////////////////////
//
//  ADOBE SYSTEMS INCORPORATED
//  Copyright 2005-2007 Adobe Systems Incorporated
//  All Rights Reserved.
//
//  NOTICE: Adobe permits you to use, modify, and distribute this file
//  in accordance with the terms of the license agreement accompanying it.
//
////////////////////////////////////////////////////////////////////////////////

package mx.core
{

import flash.display.DisplayObjectContainer;
import flash.events.Event;
import flash.geom.Matrix;

/**
 *  SpriteAsset is a subclass of the flash.display.Sprite class which
 *  represents vector graphic images that you embed in an application.
 *  It implements the IFlexDisplayObject interface, which makes it
 *  possible for an embedded vector graphic image to be displayed in an Image
 *  control, or to be used as a container background or a component skin.
 *
 *  <p>The vector graphic image that you're embedding can be in an SVG file.
 *  You can also embed a sprite symbol that is in a SWF file produced
 *  by Flash.
 *  In both cases, the MXML compiler autogenerates a class that extends
 *  SpriteAsset to represent the embedded vector graphic image.</p>
 *
 *  <p>You don't generally have to use the SpriteAsset class directly
 *  when you write a Flex application.
 *  For example, you can embed a sprite symbol from a SWF file and display
 *  it in an Image control by writing the following:</p>
 *
 *  <pre>
 *  &lt;mx:Image id="logo" source="&#64;Embed(source='Assets.swf', symbol='Logo')"/&gt;</pre>
 *
 *  <p>Or use it as the application's background image in CSS syntax
 *  by writing the following:</p>
 *
 *  <pre>
 *  &lt;fx:Style&gt;
 *      &#64;namespace mx "library://ns.adobe.com/flex/mx"
 *      mx|Application {
 *          backgroundImage: Embed(source="Assets.swf", symbol='Logo')
 *      }
 *  &lt;fx:Style/&gt;</pre>
 *
 *  <p>without having to understand that the MXML compiler has created
 *  a subclass of BitmapAsset for you.</p>
 *
 *  <p>However, it may be useful to understand what is happening
 *  at the ActionScript level.
 *  To embed a vector graphic image in ActionScript, you declare a variable
 *  of type Class, and put <code>[Embed]</code> metadata on it.
 *  For example, you embed a sprite symbol from a SWF file like this:</p>
 *
 *  <pre>
 *  [Bindable]
 *  [Embed(source="Assets.swf", symbol="Logo")]
 *  private var logoClass:Class;</pre>
 *
 *  <p>The MXML compiler notices that the Logo symbol in Assets.swf
 *  is a sprite, autogenerates a subclass of the SpriteAsset class
 *  to represent it, and sets your variable to be a reference to this
 *  autogenerated class.
 *  You can then use this class reference to create instances of the
 *  SpriteAsset using the <code>new</code> operator, and use APIs
 *  of the Sprite class on them:</p>
 *
 *  <pre>
 *  var logo:SpriteAsset = SpriteAsset(new logoClass());
 *  logo.rotation=45;</pre>
 *
 *  <p>However, you rarely need to create SpriteAsset instances yourself
 *  because image-related properties and styles can simply be set to an
 *  image-producing class, and components will create image instances
 *  as necessary.
 *  For example, to display this vector graphic image in an Image control,
 *  you can set the Image's <code>source</code> property to
 *  <code>logoClass</code>.
 *  In MXML you could do this as follows:</p>
 *
 *  <pre>
 *  &lt;mx:Image id="logo" source="{logoClass}"/&gt;</pre>
 *  
 *  @langversion 3.0
 *  @playerversion Flash 9
 *  @playerversion AIR 1.1
 *  @productversion Flex 3
 */
public class SpriteAsset extends FlexSprite
                         implements IFlexAsset, IFlexDisplayObject, IBorder,
                                    ILayoutDirectionElement
{
    include "../core/Version.as";

    //--------------------------------------------------------------------------
    //
    //  Constructor
    //
    //--------------------------------------------------------------------------

    /**
     *  Constructor.
     *  
     *  @langversion 3.0
     *  @playerversion Flash 9
     *  @playerversion AIR 1.1
     *  @productversion Flex 3
     */
    public function SpriteAsset()
    {
        super();

        // Remember initial size as our measured size.
        _measuredWidth = width;
        _measuredHeight = height;
        
        if (FlexVersion.compatibilityVersion >= FlexVersion.VERSION_4_0)
            this.addEventListener(Event.ADDED, addedHandler);
    }

    //--------------------------------------------------------------------------
    //
    //  Variables
    //
    //--------------------------------------------------------------------------
    
    private var mirror:Boolean = false;
    private var origMatrix:Matrix;
    
    //--------------------------------------------------------------------------
    //
    //  Overridden Properties
    //
    //--------------------------------------------------------------------------
    
    //----------------------------------
    //  x
    //----------------------------------
    
    private var _x:Number = 0;
    
    /**
     *  @private
     *  In the mirroring case, we restore the old transform matrix,
     *  call the superclass' setter to recalculate the transform matrix,
     *  and call validateTransformMatrix to de-mirror the matrix.
     *  This ensures that the right matrix values are used when 
     *  de-mirroring.
     */
    override public function set x(value:Number):void
    {
        if (mirror)
        {
            transform.matrix = origMatrix;
            super.x = value;
            _x = value;
            validateTransformMatrix();
        }
        else
        {
            super.x = value;
        }
    }
    
    /**
     *  @private
     */
    override public function get x():Number
    {
        // FIXME(hmuller): by default get x returns transform.matrix.tx rounded to the nearest 20th.
        // should do the same here, if we're returning _x.
        return (mirror) ? _x : super.x;
    }
    
    //----------------------------------
    //  width
    //----------------------------------
    
    /**
     *  @private
     */
    override public function set width(value:Number):void
    {
        if (mirror)
        {
            transform.matrix = origMatrix;
            super.width = value;
            // Store new scaleX/Y since setting width may modify them
            _scaleX = super.scaleX;
            _scaleY = super.scaleY;
            validateTransformMatrix();
        }
        else
        {
            super.width = value;
        }
    }
    
    //----------------------------------
    //  height
    //----------------------------------
    
    /**
     *  @private
     *  We must override height as well because setting
     *  height will force scaleX to be positive in the transform
     *  matrix.
     */
    override public function set height(value:Number):void  
    {
        if (mirror)
        {
            transform.matrix = origMatrix;
            super.height = value;
            // Store new scaleX/Y since setting height may modify them
            _scaleX = super.scaleX;
            _scaleY = super.scaleY;
            validateTransformMatrix();
        }
        else
        {
            super.height = value;
        }
    }
    
    //----------------------------------
    //  scaleX
    //----------------------------------
    
    private var _scaleX:Number;
    
    /**
     *  @private
     */
    override public function set scaleX(value:Number):void
    {
        if (mirror)
        {
            transform.matrix = origMatrix;
            super.scaleX = value;
            _scaleX = value;
            validateTransformMatrix();
        }
        else
        {
            super.scaleX = value;
        }
    }
    
    override public function get scaleX():Number
    {
        return (mirror) ? _scaleX : super.scaleX;
    }
    
    //----------------------------------
    //  scaleY
    //----------------------------------
    
    private var _scaleY:Number;
    
    /**
     *  @private
     */
    override public function set scaleY(value:Number):void
    {
        if (mirror)
        {
            transform.matrix = origMatrix;
            super.scaleY = value;
            _scaleY = value;
            validateTransformMatrix();
        }
        else
        {
            super.scaleY = value;
        }
    }
    
    override public function get scaleY():Number
    {
        return (mirror) ? _scaleY : super.scaleY;
    }

    //--------------------------------------------------------------------------
    //
    //  Properties
    //
    //--------------------------------------------------------------------------

    //----------------------------------
    //  layoutDirection
    //----------------------------------
    
    private var _layoutDirection:String = LayoutDirection.LTR;
    
    /**
     *  @inheritDoc
     *  
     *  @langversion 3.0
     *  @playerversion Flash 10
     *  @playerversion AIR 1.5
     *  @productversion Flex 4.1
     */
    public function get layoutDirection():String
    {
        return _layoutDirection;
    }
    
    public function set layoutDirection(value:String):void
    {
        if (value == _layoutDirection)
            return;
        
        _layoutDirection = value;
        invalidateLayoutDirection();
    }

    //----------------------------------
    //  measuredHeight
    //----------------------------------

    /**
     *  @private
     *  Storage for the measuredHeight property.
     */
    private var _measuredHeight:Number;

    /**
     *  @inheritDoc
     *  
     *  @langversion 3.0
     *  @playerversion Flash 9
     *  @playerversion AIR 1.1
     *  @productversion Flex 3
     */
    public function get measuredHeight():Number
    {
        return _measuredHeight;
    }

    //----------------------------------
    //  measuredWidth
    //----------------------------------

    /**
     *  @private
     *  Storage for the measuredWidth property.
     */
    private var _measuredWidth:Number;

    /**
     *  @inheritDoc
     *  
     *  @langversion 3.0
     *  @playerversion Flash 9
     *  @playerversion AIR 1.1
     *  @productversion Flex 3
     */
    public function get measuredWidth():Number
    {
        return _measuredWidth;
    }

    //----------------------------------
    //  borderMetrics
    //----------------------------------
    
    /**
     *  @inheritDoc
     *  
     *  @langversion 3.0
     *  @playerversion Flash 9
     *  @playerversion AIR 1.1
     *  @productversion Flex 3
     */
    public function get borderMetrics():EdgeMetrics
    {       
        if (scale9Grid == null)
        {
            return EdgeMetrics.EMPTY;   
        }
        else
        {
            return new EdgeMetrics(scale9Grid.left,
                                   scale9Grid.top,
                                   Math.ceil(measuredWidth - scale9Grid.right),
                                   Math.ceil(measuredHeight - scale9Grid.bottom));  
        }
    }
    
    //--------------------------------------------------------------------------
    //
    //  Methods
    //
    //--------------------------------------------------------------------------

    /**
     *  @inheritDoc
     *  
     *  @langversion 3.0
     *  @playerversion Flash 10
     *  @playerversion AIR 1.5
     *  @productversion Flex 4.1
     */
    public function invalidateLayoutDirection():void
    {
        // We check the closest parent's layoutDirection property
        // set our mirror property and update our transform matrix
        // accordingly.
        var p:DisplayObjectContainer = parent;
        
        while (p)
        {
            if (p is ILayoutDirectionElement)
            {
                // If this element's layoutDirection doesn't match its parent's, then
                // set the mirror flag.
                const oldMirror:Boolean = mirror;
                mirror = (_layoutDirection != null) &&
                    (_layoutDirection != ILayoutDirectionElement(p).layoutDirection);
                
                if (mirror != oldMirror)
                {
                    if (mirror)
                    {
                        // Set backing variables to current state
                        _scaleX = super.scaleX;
                        _scaleY = super.scaleY;
                        _x = super.x;
                        validateTransformMatrix();
                    }
                    else
                    {
                        // origMatrix has been initialized in validateTransformMatrix
                        // because we weren't matching our parent's layoutDirection before.
                        transform.matrix = origMatrix;
                    }
                }
                break;
            }
            
            p = p.parent;
        }
    }
    
    /**
     *  @inheritDoc
     *  
     *  @langversion 3.0
     *  @playerversion Flash 9
     *  @playerversion AIR 1.1
     *  @productversion Flex 3
     */
    public function move(x:Number, y:Number):void
    {
        this.x = x;
        this.y = y;
    }

    /**
     *  @inheritDoc
     *  
     *  @langversion 3.0
     *  @playerversion Flash 9
     *  @playerversion AIR 1.1
     *  @productversion Flex 3
     */
    public function setActualSize(newWidth:Number, newHeight:Number):void
    {
        width = newWidth;
        height = newHeight;
    }
    
    /**
     *  @private
     */
    private function addedHandler(event:Event):void
    {
        invalidateLayoutDirection();
    }
    
    /**
     *  @private
     *  Modifies the transform matrix so that this bitmap
     *  will not be mirrored if a parent is mirrored.
     */
    private function validateTransformMatrix():void
    {
        // Save copy of current matrix
        origMatrix = transform.matrix.clone();
        
        // Create new de-mirrored transform matrix
        const mirrorMatrix:Matrix = transform.matrix;
        mirrorMatrix.translate(-mirrorMatrix.tx, 0);
        mirrorMatrix.scale(-1, 1);
        mirrorMatrix.translate(_x + width, 0);
        
        transform.matrix = mirrorMatrix;
    }
}

}
